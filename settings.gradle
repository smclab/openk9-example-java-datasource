pluginManagement {
	plugins {
		id 'biz.aQute.bnd.builder' version '5.2.0'
		id "org.sonarqube" version "3.0"
	}
}

rootProject.name = 'example-datasource'

import static groovy.io.FileVisitResult.*
import static groovy.io.FileType.*

def skipDirs = ~/^(build|\..*|src|out)/
def preDir = {
	if (skipDirs.matcher(it.name).matches())
		return SKIP_SUBTREE
}

def getProjectName(String dir) {

	String separator = File.separator == "\\" ? "\\" + File.separator : File.separator

	return dir.minus(rootDir.toString() + File.separator).replaceAll(separator, ":")
}

rootDir.traverse(
		type: DIRECTORIES,
		preDir: preDir) { dir ->
	def dstr = dir.toString()
	if ((!skipDirs.matcher(dir.name)) &&
		(new File(dstr + File.separator + "bnd.bnd").exists() || new File(dstr + File.separator + "build.gradle").exists())) {
		include getProjectName(dstr)
	}
}
