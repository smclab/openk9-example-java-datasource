buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.2.0"
		classpath "com.github.node-gradle:gradle-node-plugin:3.0.1"
	}
}

apply plugin: 'java'
apply plugin: 'biz.aQute.bnd.builder'

apply from: "$rootDir/build-publishing.gradle"

sourceCompatibility = 11
targetCompatibility = 11

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile group: 'io.openk9', name: 'io.openk9.release.api', version: '0.0.1-alpha-1'
	compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
}

apply plugin: 'com.github.node-gradle.node'
node {
	nodeProjectDir = file("${project.projectDir}/js")
	download = true
}

task installDeps(type: YarnTask) {
    args = ['install']
}

task buildJS(type: YarnTask) {
    dependsOn installDeps
    args = ['build']
}

task copyJS(type: Copy) {
    dependsOn buildJS
    from 'js/build'
    into 'src/main/resources/build'
}
build.dependsOn(copyJS)

task cleanJS(type: Delete) {
	delete("${project.projectDir}/js/build")
	delete("${project.projectDir}/js/node_modules")
	delete("${project.projectDir}/src/main/resources/build")
}
clean.dependsOn(cleanJS)
